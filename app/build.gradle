plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

configurations {
    ktlint
}

android {
    compileSdk 33
    sourceSets.all {
        kotlin.srcDir("src/$name/kotlin")
    }

    defaultConfig {
        applicationId "com.fakedevelopers.bidderbidder"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    // ktlint
    ktlint 'com.pinterest:ktlint:0.47.1'
    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:30.5.0')
    implementation 'com.google.firebase:firebase-analytics-ktx:21.1.1'
    implementation 'com.google.firebase:firebase-auth-ktx:21.0.8'
    implementation 'com.google.android.gms:play-services-auth:20.3.0'
    // Logger
    implementation 'com.orhanobut:logger:2.2.0'
    // REST API 통신
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    // Converter (JSON 타입 결과를 객체로 매핑)
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    // Converter (결과가 그냥 문자열 일 때)
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    // okhttp3
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.6'
    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    // Hilt
    implementation 'com.google.dagger:hilt-android:2.44'
    kapt 'com.google.dagger:hilt-android-compiler:2.44'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    kapt 'com.github.bumptech.glide:compiler:4.13.2'
    // time 라이브러리 백포트
    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.1'
    // datastore
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    // viewpager2
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    // Sentry
    implementation 'io.sentry:sentry-android:6.4.2'
    implementation "io.getstream:stream-chat-android-ui-components:5.11.0"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass.set("com.pinterest.ktlint.Main")
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    mainClass.set("com.pinterest.ktlint.Main")
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
